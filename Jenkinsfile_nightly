#!groovy

properties([
  pipelineTriggers([cron('H 17 * * 1-5')]),
  disableConcurrentBuilds(),
  durabilityHint('MAX_SURVIVABILITY'),
  parameters([
    string(
      name: 'BASE_URL',
      defaultValue: 'https://ccdcspreprod.caselines.co.uk/',
      description: 'The Base URL to test against'
    ),
    string(
      name: 'FUNCTIONAL_TESTS_WORKERS',
      defaultValue: '4',
      description: 'Number of workers running functional tests'
    ),
    string(
      name: 'TAGS_TO_RUN',
      defaultValue: '',
      description: 'Optionally, run a single or multiple tags (comma separated e.g. @cui, @exui)'
    ),
    choice(
      name: 'BROWSER_TO_RUN',
      choices: ['chromium', 'chrome', 'firefox', 'webkit', 'edge', 'tabletchrome', 'tabletwebkit'],
      description: 'Choose what browsers will be run (only when a tag is specified)'
    ),
  ])
])

@Library("Infrastructure")

def type = "nodejs"
def product = "dcs-automation"
def component = "sds"
def channel = "#dcs-automation"

static Map < String, Object > secret(String secretName, String envVariable) {
  [
    $class: 'AzureKeyVaultSecret',
    secretType: 'Secret',
    name: secretName,
    envVariable: envVariable
  ]
}

def secrets = [
  'dcs-automation-bts-stg': [
    secret('ADMIN-HMCTS-PASSWORD', 'HMCTS_ADMIN_PASSWORD'),
    secret('BASE-URL-PREPROD', 'BASE_URL_PREPROD'),
    secret('BASE-URL-UAT', 'BASE_URL_UAT'),
    secret('CPSADMIN-PASSWORD', 'CPS_ADMIN_PASSWORD'),
    secret('COOKIE', 'COOKIE'),
    secret('CPSPROSECUTOR-PASSWORD', 'CPS_PROSECUTOR_PASSWORD'),
    secret('DEFENCE-A-PASSWORD', 'DEFENCE_ADVOCATE_A_PASSWORD'),
    secret('DEFENCE-B-PASSWORD', 'DEFENCE_ADVOCATE_B_PASSWORD'),
    secret('DEFENCE-C-PASSWORD', 'DEFENCE_ADVOCATE_C_PASSWORD'),
    secret('FT-JUDGE-PASSWORD', 'FT_JUDGE_PASSWORD'),
    secret('PROBATION-STAFF-PASSWORD', 'PROBATION_STAFF_PASSWORD'),
    secret('TESTAC-PASSWORD', 'TESTAC_PASSWORD'),
    secret('USER-REG-PASSWORD', 'USER_REG_PASSWORD'),
    secret('ADMIN-PASSWORD', 'ADMIN_PASSWORD'),
    secret('PREPROD-UPLOAD-PTPH-FORM-URL', 'PREPROD_UPLOAD_PTPH_FORM_URL'),
    secret('PREPROD-CONFIRM-PTPH-UPLOAD-STATUS-URL', 'PREPROD_CONFIRM_PTPH_UPLOAD_STATUS_URL'),
    secret('UAT-UPLOAD-PTPH-FORM-URL', 'UAT_UPLOAD_PTPH_FORM_URL'),
    secret('UAT-CONFIRM-PTPH-UPLOAD-STATUS-URL', 'UAT_CONFIRM_PTPH_UPLOAD_STATUS_URL')
  ]
]

def buildPlaywrightCommand(tags, browser) {
  if (!tags?.trim()) return
  def tagList = tags.split(',').collect { it.trim() }
  def command = 'playwright test tests/'
  tagList.each { tag ->
    if (tag) command += " --grep ${tag}"
  }
  if (browser) command += " --project=${browser}"
  return command
}

def yarnBuilder = new uk.gov.hmcts.contino.YarnBuilder(this)

withNightlyPipeline(type, product, component, 600) {
  loadVaultSecrets(secrets)
  env.BASE_URL = params.BASE_URL
  env.FUNCTIONAL_TESTS_WORKERS = params.FUNCTIONAL_TESTS_WORKERS

  def isFullRegressionRun = params.TAGS_TO_RUN?.trim() == '@regression'

  env.TEST_USERS = isFullRegressionRun ? 'regression' : 'nightly'
  
  if (env.TEST_USERS == 'regression') {
    env.TEST_TAG = '@regression'
  } else {
    env.TEST_TAG = '@nightly'
  }

  echo "Running with TEST_USERS=${env.TEST_USERS}"
  echo "Running with TEST_TAG=${env.TEST_TAG}"
  
  enableSlackNotifications(channel)

  // ---- USER ROTATION (Stateless & Deterministic) ----
  def eligibleUsers = [
      'HMCTSAdmin',
      'CPSAdmin',
      'CPSProsecutor',
      'DefenceAdvocateA',
      'FullTimeJudge',
      'ProbationStaff'
  ]
  
  // Use the current day of the year to pick a user
  def dayOfYear = Calendar.getInstance().get(Calendar.DAY_OF_YEAR)
  def userIndex = dayOfYear % eligibleUsers.size()
  
  def nextUser = eligibleUsers[userIndex]
  
  env.TEST_USER = nextUser
  echo "Nightly Playwright tests will run as user: ${env.TEST_USER} (Determined by day of year)"
  // ------------------------

  stage('Clean Sessions') {
    node {
      def sessionsDir = "${env.WORKSPACE}/playwright-e2e/.sessions"
      if (fileExists(sessionsDir)) {
        echo "Deleting old sessions folder..."
        dir(sessionsDir) { deleteDir() }
      } else {
        echo "No sessions folder to delete"
      }
    }
  }

  afterAlways('DependencyCheckNightly') {

    stage('Set up playwright') {
      try { yarnBuilder.yarn('setup') }
      catch (Error) { unstable("${STAGE_NAME} is unstable: ${Error}") }
    }

    if (params.TAGS_TO_RUN?.trim() && !isFullRegressionRun) {
      stage("${params.TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}") {
        try {
          currentBuild.displayName = "${params.TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}"
          if (params.BROWSER_TO_RUN == 'edge') yarnBuilder.yarn('setup:edge')
          yarnBuilder.yarn(buildPlaywrightCommand(params.TAGS_TO_RUN, params.BROWSER_TO_RUN))
          // USE TO UPDATE SCREENSHOTS
          // yarnBuilder.yarn(buildPlaywrightCommand(params.TAGS_TO_RUN, params.BROWSER_TO_RUN)+ " --update-snapshots")
        } catch (Error) {
          unstable("${STAGE_NAME} is unstable: ${Error}")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: "${params.TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}"
          ])
          // archiveArtifacts artifacts: 'playwright-e2e/tests/**/*-snapshots/**/*.png',
          //          allowEmptyArchive: true
        }
      }
    } else {
      currentBuild.displayName = "All E2E Tests"

      //Cross-browser smoke tests for critical functionality
      stage('Smoke Tests') {
        try {
          yarnBuilder.yarn('setup:edge')
          yarnBuilder.yarn('test:smoke')
        } catch (Error) {
          unstable("Smoke tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Smoke Tests'
          ])
        }
      }

      // Unified Chrome Tests: All tests, with notes lifecycle running last
      stage('Chrome – E2E Tests') {
        try {
          yarnBuilder.yarn('test:chrome')
        } catch (Error) {
          unstable("Chrome E2E tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Chrome – E2E Tests'
          ])
        }
      }

      // Unified Firefox Tests: All tests, with notes lifecycle running last
      stage('Firefox – E2E Tests') {
        try {
          yarnBuilder.yarn('test:firefox')
        } catch (Error) {
          unstable("Firefox E2E tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Firefox – E2E Tests'
          ])
        }
      }

      stage('Generate & Publish Allure Report') {
        try {
          sh 'yarn allure:post-process'
          sh 'yarn allure:generate || true'
        } catch (Error) {
          unstable("Allure report generation failed")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "allure-report",
            reportFiles: 'index.html',
            reportName: 'Allure Test Report'
          ])
        }
      }

      // RUN THIS TO ACCESS SNAPSHOTS AS A DOWNLOADABLE ARCHIVE
      // stage('Archive PTPH Snapshots') {
      //   archiveArtifacts artifacts: 'playwright-e2e/tests/**/*-snapshots/**/*.png', allowEmptyArchive: true
      // }
    }
  }
} 
