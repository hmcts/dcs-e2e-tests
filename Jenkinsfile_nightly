#!groovy

properties([
  pipelineTriggers([cron('H 16 * * 1-5')]),
  disableConcurrentBuilds(),
  durabilityHint('MAX_SURVIVABILITY'),
  parameters([
    string(
      name: 'BASE_URL',
      defaultValue: 'https://ccdcspreprod.caselines.co.uk/',
      description: 'The Base URL to test against'
    ),
    string(
      name: 'FUNCTIONAL_TESTS_WORKERS',
      defaultValue: '4',
      description: 'Number of workers running functional tests'
    ),
    string(
      name: 'TAGS_TO_RUN',
      defaultValue: '',
      description: 'Optionally, run a single or multiple tags (comma separated e.g. @cui, @exui)'
    ),
    choice(
      name: 'BROWSER_TO_RUN',
      choices: ['chromium', 'chrome', 'firefox', 'webkit', 'edge', 'tabletchrome', 'tabletwebkit'],
      description: 'Choose what browsers will be run (only when a tag is specified)'
    ),
  ])
])

@Library("Infrastructure")

def type = "nodejs"
def product = "dcs-automation"
def component = "sds"
def channel = "#dcs-automation"

static Map < String, Object > secret(String secretName, String envVariable) {
  [
    $class: 'AzureKeyVaultSecret',
    secretType: 'Secret',
    name: secretName,
    envVariable: envVariable
  ]
}

def secrets = [
  'dcs-automation-bts-stg': [
    secret('ADMIN-HMCTS-PASSWORD', 'HMCTS_ADMIN_PASSWORD'),
    secret('BASE-URL-PREPROD', 'BASE_URL_PREPROD'),
    secret('BASE-URL-UAT', 'BASE_URL_UAT'),
    secret('CPSADMIN-PASSWORD', 'CPS_ADMIN_PASSWORD'),
    secret('COOKIE', 'COOKIE'),
    secret('CPSPROSECUTOR-PASSWORD', 'CPS_PROSECUTOR_PASSWORD'),
    secret('DEFENCE-A-PASSWORD', 'DEFENCE_ADVOCATE_A_PASSWORD'),
    secret('DEFENCE-B-PASSWORD', 'DEFENCE_ADVOCATE_B_PASSWORD'),
    secret('DEFENCE-C-PASSWORD', 'DEFENCE_ADVOCATE_C_PASSWORD'),
    secret('FT-JUDGE-PASSWORD', 'FT_JUDGE_PASSWORD'),
    secret('PROBATION-STAFF-PASSWORD', 'PROBATION_STAFF_PASSWORD'),
    secret('TESTAC-PASSWORD', 'TESTAC_PASSWORD'),
    secret('USER-REG-PASSWORD', 'USER_REG_PASSWORD'),
    secret('ADMIN-PASSWORD', 'ADMIN_PASSWORD'),
    secret('PREPROD-UPLOAD-PTPH-FORM-URL', 'PREPROD_UPLOAD_PTPH_FORM_URL'),
    secret('PREPROD-CONFIRM-PTPH-UPLOAD-STATUS-URL', 'PREPROD_CONFIRM_PTPH_UPLOAD_STATUS_URL'),
    secret('UAT-UPLOAD-PTPH-FORM-URL', 'UAT_UPLOAD_PTPH_FORM_URL'),
    secret('UAT-CONFIRM-PTPH-UPLOAD-STATUS-URL', 'UAT_CONFIRM_PTPH_UPLOAD_STATUS_URL')
  ]
]

def buildPlaywrightCommand(tags, browser) {
  if (!tags?.trim()) return
  def tagList = tags.split(',').collect { it.trim() }
  def command = 'playwright test tests/'
  tagList.each { tag ->
    if (tag) command += " --grep ${tag}"
  }
  if (browser) command += " --project=${browser}"
  return command
}

def yarnBuilder = new uk.gov.hmcts.contino.YarnBuilder(this)

withNightlyPipeline(type, product, component, 600) {
  loadVaultSecrets(secrets)
  env.BASE_URL = params.BASE_URL
  env.FUNCTIONAL_TESTS_WORKERS = params.FUNCTIONAL_TESTS_WORKERS

  def isFullRegressionRun = TAGS_TO_RUN?.trim() == '@regression'

  env.TEST_USERS = isFullRegressionRun ? 'regression' : 'nightly'
  
  if (env.TEST_USERS == 'regression') {
    env.TEST_TAG = '@regression'
    env.TEST_TAG_REGEX = '(?=.*@regression)'
  } else {
    env.TEST_TAG = '@nightly'
    env.TEST_TAG_REGEX = '(?=.*@nightly)'
  }

  echo "Running with TEST_USERS=${env.TEST_USERS}"
  echo "Running with TEST_TAG=${env.TEST_TAG}"
  
  enableSlackNotifications(channel)

  // ---- USER ROTATION ----
  def eligibleUsers = [
      'HMCTSAdmin',
      'CPSAdmin',
      'CPSProsecutor',
      'DefenceAdvocateA',
      'FullTimeJudge',
      'ProbationStaff'
  ]
  
  def nextUser
  node {
      def lastUserIndexFile = "${env.WORKSPACE}/last_user_index.txt"
      def lastIndex = -1

      if (fileExists(lastUserIndexFile)) {
          lastIndex = readFile(lastUserIndexFile).trim().toInteger()
      }

      def nextIndex = (lastIndex + 1) % eligibleUsers.size()
      nextUser = eligibleUsers[nextIndex]

      writeFile file: lastUserIndexFile, text: nextIndex.toString()
  }
  env.TEST_USER = nextUser
  echo "Nightly Playwright tests will run as user: ${env.TEST_USER}"

  // ------------------------

  stage('Clean Sessions') {
    node {
      def sessionsDir = "${env.WORKSPACE}/playwright-e2e/.sessions"
      if (fileExists(sessionsDir)) {
        echo "Deleting old sessions folder..."
        dir(sessionsDir) { deleteDir() }
      } else {
        echo "No sessions folder to delete"
      }
    }
  }

  afterAlways('DependencyCheckNightly') {

    stage('Set up playwright') {
      try { yarnBuilder.yarn('setup') }
      catch (Error) { unstable("${STAGE_NAME} is unstable: ${Error}") }
    }

    if (TAGS_TO_RUN?.trim() && !isFullRegressionRun) {
      stage("${TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}") {
        try {
          currentBuild.displayName = "${TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}"
          if (params.BROWSER_TO_RUN == 'edge') yarnBuilder.yarn('setup:edge')
          yarnBuilder.yarn(buildPlaywrightCommand(TAGS_TO_RUN, params.BROWSER_TO_RUN))
          // USE TO UPDATE SCREENSHOTS
          // yarnBuilder.yarn(buildPlaywrightCommand(TAGS_TO_RUN, params.BROWSER_TO_RUN)+ " --update-snapshots")
        } catch (Error) {
          unstable("${STAGE_NAME} is unstable: ${Error}")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: "${TAGS_TO_RUN} E2E Tests for ${params.BROWSER_TO_RUN}"
          ])
          // archiveArtifacts artifacts: 'playwright-e2e/tests/**/*-snapshots/**/*.png',
          //          allowEmptyArchive: true
        }
      }
    } else {
      currentBuild.displayName = "All E2E Tests"

      stage('Chrome – Multi-worker E2E Tests') {
        try {
          yarnBuilder.yarn('test:chrome')
        } catch (Error) {
          unstable("Chrome multi-worker tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Chrome – Multi-worker'
          ])
        }
      }

      stage('Chrome – Single-worker (@notes)') {
        try {
          yarnBuilder.yarn('test:notes --project=chrome')
        } catch (Error) {
          unstable("Chrome single-worker (@notes) tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Chrome – Single-worker (@notes)'
          ])
        }
      }

      stage('Firefox – Multi-worker E2E Tests') {
        try {
          yarnBuilder.yarn('test:firefox')
        } catch (Error) {
          unstable("Firefox multi-worker tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Firefox – Multi-worker'
          ])
        }
      }

      stage('Firefox – Single-worker (@notes)') {
        try {
          yarnBuilder.yarn('test:notes --project=firefox')
        } catch (Error) {
          unstable("Firefox single-worker (@notes) tests had failures")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "playwright-report",
            reportFiles: 'index.html',
            reportName: 'Firefox – Single-worker (@notes)'
          ])
        }
      }

      stage('Generate & Publish Allure Report') {
        try {
          sh 'yarn allure:generate || true'
        } catch (Error) {
          unstable("Allure report generation failed")
        } finally {
          publishHTML([
            allowMissing: true,
            alwaysLinkToLastBuild: true,
            keepAll: true,
            reportDir: "allure-report",
            reportFiles: 'index.html',
            reportName: 'Allure Test Report'
          ])
        }
      }

      // RUN THIS TO ACCESS SNAPSHOTS AS A DOWNLOADABLE ARCHIVE
      // stage('Archive PTPH Snapshots') {
      //   archiveArtifacts artifacts: 'playwright-e2e/tests/**/*-snapshots/**/*.png', allowEmptyArchive: true
      // }
    }
  } 
} 
